<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="FB_XBox_Single_Button_NC" Id="{33a2eeaa-a1e7-4d1f-b008-50b37140f1b8}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_XBox_Single_Button_NC
VAR
	_mcJog				: MC_Jog;
	_rtJogEnable		: R_TRIG;
	_ftJogEnable		: F_TRIG;
	_bJoggingActive		: BOOL;
	_fPrevOverride		: UDINT;
	_RampTimer			: TON;
	_fCurrentOverride   : LREAL;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="FB_init" Id="{1a916281-ca52-4617-a5d5-ab2c81fc838a}">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="Jog" Id="{74cc8ee7-bc62-4be0-a73b-c97214129252}">
      <Declaration><![CDATA[METHOD Jog : BOOL
VAR_INPUT
	bEnable			: BOOL;	
	bButton			: BOOL;
	Axis			: REFERENCE TO Axis_Ref;
	stOptions		: ST_XBox_Button_NC_Options;
END_VAR
VAR
	bJogForward 	 : BOOL;
	bJogBackwards 	 : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Rising edge trigger of the block enable
_rtJogEnable(CLK := bEnable);
// Falling edge trigger of the block enable
_ftJogEnable(CLK := bEnable);

// Rising edge trigger of the block enable
IF _rtJogEnable.Q THEN
	// Save the current override value before jogging
	// Check the Axis reference is valid	
	IF __ISVALIDREF(Axis) THEN
		THIS^._fPrevOverride := Axis.PlcToNc.Override;
	END_IF
	// Enable the jog methods
	_bJoggingActive	:= TRUE;
END_IF

// Falling edge trigger of the block enable
IF _ftJogEnable.Q THEN
	// Restore the past override value before enable
	// Check the Axis reference is valid	
	IF __ISVALIDREF(Axis) THEN
		Axis.PlcToNc.Override := THIS^._fPrevOverride;
			// Cleanup any old Jog commands
		THIS^._mcJog(
					  Axis:= Axis, 
					  JogForward:= FALSE, 
					  JogBackwards:= FALSE, 
					  Mode:= E_JogMode.MC_JOGMODE_STANDARD_FAST);	
	END_IF
	// Disable calling to jog methods
	_bJoggingActive := FALSE;
END_IF

// Check the Joystick reference is valid
IF _bJoggingActive THEN
	// Check the Axis reference is valid	
	IF __ISVALIDREF(Axis) THEN
		// Speed Ramp Timer
		_RampTimer(IN:=stOptions.bRampedOverride AND NOT _RampTimer.Q, PT:= stOptions.tRampStepTime);
		
		// Check the direction the joystick is moving, or if in deadzone					 
		IF bButton = FALSE THEN
			bJogForward 	:= FALSE;
			bJogBackwards := FALSE;
			Axis.PlcToNc.Override := 0;
			_fCurrentOverride := 0;
		ELSE
			// Check for inversion
			IF NOT stOptions.bInverted THEN
				bJogForward 	:= TRUE;
				bJogBackwards 	:= FALSE;
			ELSE
				bJogForward 	:= FALSE;
				bJogBackwards 	:= TRUE;
			END_IF
			
			// Find the override speed

			IF _RampTimer.Q AND stOptions.bRampedOverride THEN
				_fCurrentOverride := _fCurrentOverride + stOptions.iRampStepFactor;		
			ELSIF NOT stOptions.bRampedOverride THEN
				_fCurrentOverride := stOptions.fMaxOverride;
			END_IF
			// Check range
			IF _fCurrentOverride > stOptions.fMaxOverride THEN
				_fCurrentOverride := stOptions.fMaxOverride;
			END_IF
			
			Axis.PlcToNc.Override := TO_UDINT(ABS(_fCurrentOverride) * 10000);
		END_IF
		
		// Main jog call for axis
		THIS^._mcJog(
					  Axis:= Axis, 
					  JogForward:= bJogForward, 
					  JogBackwards:= bJogBackwards, 
					  Mode:= E_JogMode.MC_JOGMODE_STANDARD_FAST);
	END_IF
END_IF]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_XBox_Single_Button_NC">
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_XBox_Single_Button_NC.FB_init">
      <LineId Id="7" Count="0" />
    </LineIds>
    <LineIds Name="FB_XBox_Single_Button_NC.Jog">
      <LineId Id="12" Count="36" />
      <LineId Id="120" Count="2" />
      <LineId Id="49" Count="4" />
      <LineId Id="123" Count="0" />
      <LineId Id="54" Count="8" />
      <LineId Id="118" Count="0" />
      <LineId Id="112" Count="0" />
      <LineId Id="84" Count="0" />
      <LineId Id="98" Count="0" />
      <LineId Id="129" Count="0" />
      <LineId Id="87" Count="0" />
      <LineId Id="113" Count="0" />
      <LineId Id="86" Count="0" />
      <LineId Id="127" Count="0" />
      <LineId Id="124" Count="2" />
      <LineId Id="128" Count="0" />
      <LineId Id="119" Count="0" />
      <LineId Id="65" Count="8" />
      <LineId Id="5" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>